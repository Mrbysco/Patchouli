buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.23.3"
    }
}

plugins {
    // Required for NeoGradle
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'maven-publish'
    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
    java.withSourcesJar()
    archivesBaseName = "${mod_name}"

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 21
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        it.enabled = false
    }

    pmd {
        toolVersion '6.35.0'
        incrementalAnalysis.set(true)
        ruleSetFiles = rootProject.files("spotless/pmd-ruleset.xml")
    }

    jar {
        from "LICENSE"

        exclude "**/*.bat"
        exclude "**/*.psd"
        exclude "**/*.exe"
        exclude "**/unused"

        // exclude test data
        exclude "**/assets/patchouli/patchouli_books/**"
        exclude "**/data/patchouli/patchouli_books/**"
        exclude "**/data/patchouli/advancements/**"
        exclude "**/data/patchouli/recipes/**"
    }

    spotless {
        enforceCheck = false // workaround around jenkins having wrong line endings in some files
        // we explicitly check in github actions builds
        java {
            indentWithTabs()
            eclipse().configFile rootProject.file('spotless/eclipseformat.xml')
            removeUnusedImports()
            importOrderFile rootProject.file('spotless/eclipse.importorder')
        }
    }
}
